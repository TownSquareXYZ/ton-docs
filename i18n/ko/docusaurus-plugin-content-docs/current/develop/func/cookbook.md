# FunC ìš”ë¦¬ì±…

í€ì”¨ ì¿¡ë¶ì„ ë§Œë“  í•µì‹¬ ì´ìœ ëŠ” í€ì”¨ ê°œë°œìë“¤ì˜ ëª¨ë“  ê²½í—˜ì„ í•œê³³ì— ëª¨ì•„ ë¯¸ë˜ì˜ ê°œë°œìë“¤ì´ í™œìš©í•  ìˆ˜ ìˆë„ë¡ í•˜ê¸° ìœ„í•´ì„œì…ë‹ˆë‹¤!

í€ì”¨ ë¬¸ì„œ](/develop/func/types)ì— ë¹„í•´ ì´ ë¬¸ì„œëŠ” ëª¨ë“  í€ì”¨ ê°œë°œìê°€ ìŠ¤ë§ˆíŠ¸ ì»¨íŠ¸ë™íŠ¸ë¥¼ ê°œë°œí•˜ëŠ” ë™ì•ˆ í•´ê²°í•´ì•¼ í•˜ëŠ” ì¼ìƒì ì¸ ì‘ì—…ì— ë” ì´ˆì ì„ ë§ì¶”ê³  ìˆìŠµë‹ˆë‹¤.

## ê¸°ë³¸ ì‚¬í•­

### if ë¬¸ì„ ì‘ì„±í•˜ëŠ” ë°©ë²•

ì–´ë–¤ ì´ë²¤íŠ¸ê°€ ê´€ë ¨ì„±ì´ ìˆëŠ”ì§€ í™•ì¸í•˜ê³  ì‹¶ë‹¤ê³  ê°€ì •í•´ ë³´ê² ìŠµë‹ˆë‹¤. ì´ë¥¼ ìœ„í•´ í”Œë˜ê·¸ ë³€ìˆ˜ë¥¼ ì‚¬ìš©í•©ë‹ˆë‹¤. FunCì—ì„œ `true`ëŠ” `-1`ì´ê³  `false`ëŠ” `0`ì´ë¼ëŠ” ê²ƒì„ ê¸°ì–µí•˜ì„¸ìš”.

```func
int flag = 0; ;; false

if (flag) { 
    ;; do something
}
else {
    ;; reject the transaction
}
```

> ğŸ’¡ ì°¸ê³ 
>
> 0`ê°’ì€`ê±°ì§“`ì´ë¯€ë¡œ ë‹¤ë¥¸ ê°’ì€ `ì°¸`ì´ ë˜ë¯€ë¡œ ì—°ì‚°ì `==\`ê°€ í•„ìš”í•˜ì§€ ì•ŠìŠµë‹ˆë‹¤.

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> [ë¬¸ì„œì—ì„œ "If ë¬¸"](/ê°œë°œ/í€í¬/ìŠ¤í…Œì´íŠ¸ë¨¼íŠ¸#if-ìŠ¤í…Œì´íŠ¸ë¨¼íŠ¸)

### ë°˜ë³µ ë£¨í”„ë¥¼ ì‘ì„±í•˜ëŠ” ë°©ë²•

ì˜ˆë¥¼ ë“¤ì–´ ì§€ìˆ˜í™”ë¥¼ ì˜ˆë¡œ ë“¤ ìˆ˜ ìˆìŠµë‹ˆë‹¤.

```func
int number = 2;
int multiplier = number;
int degree = 5;

repeat(degree - 1) {

    number *= multiplier;
}
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> [ë¬¸ì„œì˜ "ë°˜ë³µ ë£¨í”„"](/ê°œë°œ/í€í¬/ë¬¸ì¥#ë°˜ë³µ-ë£¨í”„)

### ë™ì•ˆ ë£¨í”„ë¥¼ ì‘ì„±í•˜ëŠ” ë°©ë²•

ë™ì•ˆì€ íŠ¹ì • ì‘ì—…ì„ ì–¼ë§ˆë‚˜ ìì£¼ ìˆ˜í–‰í•´ì•¼ í• ì§€ ëª¨ë¥¼ ë•Œ ìœ ìš©í•©ë‹ˆë‹¤. ì˜ˆë¥¼ ë“¤ì–´ ë‹¤ë¥¸ ì…€ì— ëŒ€í•œ ì°¸ì¡°ë¥¼ ìµœëŒ€ 4ê°œê¹Œì§€ ì €ì¥í•˜ëŠ” ê²ƒìœ¼ë¡œ ì•Œë ¤ì§„ 'ì…€'ì„ ì˜ˆë¡œ ë“¤ì–´ë³´ê² ìŠµë‹ˆë‹¤.

```func
cell inner_cell = begin_cell() ;; create a new empty builder
        .store_uint(123, 16) ;; store uint with value 123 and length 16 bits
        .end_cell(); ;; convert builder to a cell

cell message = begin_cell()
        .store_ref(inner_cell) ;; store cell as reference
        .store_ref(inner_cell)
        .end_cell();

slice msg = message.begin_parse(); ;; convert cell to slice
while (msg.slice_refs_empty?() != -1) { ;; we should remind that -1 is true
    cell inner_cell = msg~load_ref(); ;; load cell from slice msg
    ;; do something
}
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> [ë¬¸ì„œì˜ "ë™ì•ˆ ë£¨í”„"](/ê°œë°œ/í€í¬/ìŠ¤í…Œì´íŠ¸ë¨¼íŠ¸#ë™ì•ˆ-ë£¨í”„)
>
> [ë¬¸ì„œì˜ 'ì…€'](/í•™ìŠµ/ê°œìš”/ì…€)
>
> ["slice_refs_empty?()" in docs](/develop/func/stdlib#slice_refs_empty)
>
> ["store_ref()" in docs](/develop/func/stdlib#store_ref)
>
> ["begin_cell()" in docs](/develop/func/stdlib#begin_cell)
>
> ["end_cell()" in docs](/develop/func/stdlib#end_cell)
>
> ["begin_parse()" in docs](/develop/func/stdlib#begin_parse)

### do until ë£¨í”„ë¥¼ ì‘ì„±í•˜ëŠ” ë°©ë²•

ì‚¬ì´í´ì„ í•œ ë²ˆ ì´ìƒ ì‹¤í–‰í•´ì•¼ í•  ë•ŒëŠ” `í•  ë•Œê¹Œì§€`ë¥¼ ì‚¬ìš©í•©ë‹ˆë‹¤.

```func
int flag = 0;

do {
    ;; do something even flag is false (0) 
} until (flag == -1); ;; -1 is true
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> ["ë¬¸ì„œì—ì„œ ë£¨í”„ê¹Œì§€"](/ê°œë°œ/í€í¬/ìŠ¤í…Œì´íŠ¸ë¨¼íŠ¸#ìœ í‹¸-ë£¨í”„)

### ìŠ¬ë¼ì´ìŠ¤ê°€ ë¹„ì–´ ìˆëŠ”ì§€ í™•ì¸í•˜ëŠ” ë°©ë²•

ìŠ¬ë¼ì´ìŠ¤`ë¡œ ì‘ì—…í•˜ê¸° ì „ì— ì˜¬ë°”ë¥´ê²Œ ì²˜ë¦¬í•  ë°ì´í„°ê°€ ìˆëŠ”ì§€ í™•ì¸í•´ì•¼ í•©ë‹ˆë‹¤. ì´ë¥¼ ìœ„í•´ `slice_empty?()`ë¥¼ ì‚¬ìš©í•  ìˆ˜ ìˆì§€ë§Œ, ì ì–´ë„ í•˜ë‚˜ì˜ `bit`ë°ì´í„° ë˜ëŠ” í•˜ë‚˜ì˜`ref`ê°€ ìˆëŠ” ê²½ìš° `-1`(`true\`)ì„ ë°˜í™˜í•œë‹¤ëŠ” ì ì„ ê³ ë ¤í•´ì•¼ í•©ë‹ˆë‹¤.

```func
;; creating empty slice
slice empty_slice = "";
;; `slice_empty?()` returns `true`, because slice doesn't have any `bits` and `refs`
empty_slice.slice_empty?();

;; creating slice which contains bits only
slice slice_with_bits_only = "Hello, world!";
;; `slice_empty?()` returns `false`, because slice have any `bits`
slice_with_bits_only.slice_empty?();

;; creating slice which contains refs only
slice slice_with_refs_only = begin_cell()
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_empty?()` returns `false`, because slice have any `refs`
slice_with_refs_only.slice_empty?();

;; creating slice which contains bits and refs
slice slice_with_bits_and_refs = begin_cell()
    .store_slice("Hello, world!")
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_empty?()` returns `false`, because slice have any `bits` and `refs`
slice_with_bits_and_refs.slice_empty?();
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> ["slice_empty?()" in docs](/develop/func/stdlib#slice_empty)
>
> ["store_slice()" in docs](/develop/func/stdlib#store_slice)
>
> ["store_ref()" in docs](/develop/func/stdlib#store_ref)
>
> ["begin_cell()" in docs](/develop/func/stdlib#begin_cell)
>
> ["end_cell()" in docs](/develop/func/stdlib#end_cell)
>
> ["begin_parse()" in docs](/develop/func/stdlib#begin_parse)

### ìŠ¬ë¼ì´ìŠ¤ê°€ ë¹„ì–´ ìˆëŠ”ì§€ í™•ì¸í•˜ëŠ” ë°©ë²•(ë¹„íŠ¸ëŠ” ì—†ì§€ë§Œ ì°¸ì¡°ê°€ ìˆì„ ìˆ˜ ìˆìŒ)

ë§Œì•½ `ë¹„íŠ¸`ë§Œ í™•ì¸í•´ì•¼ í•˜ê³  `ìŠ¬ë¼ì´ìŠ¤`ì— `ref`ê°€ ìˆëŠ”ì§€ ì—¬ë¶€ëŠ” ì¤‘ìš”í•˜ì§€ ì•Šë‹¤ë©´ `slice_data_empty?()`ë¥¼ ì‚¬ìš©í•´ì•¼ í•©ë‹ˆë‹¤.

```func
;; creating empty slice
slice empty_slice = "";
;; `slice_data_empty?()` returns `true`, because slice doesn't have any `bits`
empty_slice.slice_data_empty?();

;; creating slice which contains bits only
slice slice_with_bits_only = "Hello, world!";
;; `slice_data_empty?()` returns `false`, because slice have any `bits`
slice_with_bits_only.slice_data_empty?();

;; creating slice which contains refs only
slice slice_with_refs_only = begin_cell()
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_data_empty?()` returns `true`, because slice doesn't have any `bits`
slice_with_refs_only.slice_data_empty?();

;; creating slice which contains bits and refs
slice slice_with_bits_and_refs = begin_cell()
    .store_slice("Hello, world!")
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_data_empty?()` returns `false`, because slice have any `bits`
slice_with_bits_and_refs.slice_data_empty?();
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> ["slice_data_empty?()" in docs](/develop/func/stdlib#slice_data_empty)
>
> ["store_slice()" in docs](/develop/func/stdlib#store_slice)
>
> ["store_ref()" in docs](/develop/func/stdlib#store_ref)
>
> ["begin_cell()" in docs](/develop/func/stdlib#begin_cell)
>
> ["end_cell()" in docs](/develop/func/stdlib#end_cell)
>
> ["begin_parse()" in docs](/develop/func/stdlib#begin_parse)

### ìŠ¬ë¼ì´ìŠ¤ê°€ ë¹„ì–´ ìˆëŠ”ì§€ í™•ì¸í•˜ëŠ” ë°©ë²•(ì°¸ì¡°ê°€ ì—†ì§€ë§Œ ë¹„íŠ¸ê°€ ìˆì„ ìˆ˜ ìˆìŒ)

ì°¸ì¡°`ì—ë§Œ ê´€ì‹¬ì´ ìˆëŠ” ê²½ìš° `slice_refs_empty?()\`ë¥¼ ì‚¬ìš©í•˜ì—¬ ì°¸ì¡°ì˜ ì¡´ì¬ ì—¬ë¶€ë¥¼ í™•ì¸í•´ì•¼ í•©ë‹ˆë‹¤.

```func
;; creating empty slice
slice empty_slice = "";
;; `slice_refs_empty?()` returns `true`, because slice doesn't have any `refs`
empty_slice.slice_refs_empty?();

;; creating slice which contains bits only
slice slice_with_bits_only = "Hello, world!";
;; `slice_refs_empty?()` returns `true`, because slice doesn't have any `refs`
slice_with_bits_only.slice_refs_empty?();

;; creating slice which contains refs only
slice slice_with_refs_only = begin_cell()
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_refs_empty?()` returns `false`, because slice have any `refs`
slice_with_refs_only.slice_refs_empty?();

;; creating slice which contains bits and refs
slice slice_with_bits_and_refs = begin_cell()
    .store_slice("Hello, world!")
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_refs_empty?()` returns `false`, because slice have any `refs`
slice_with_bits_and_refs.slice_refs_empty?();
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> ["slice_refs_empty?()" in docs](/develop/func/stdlib#slice_refs_empty)
>
> ["store_slice()" in docs](/develop/func/stdlib#store_slice)
>
> ["store_ref()" in docs](/develop/func/stdlib#store_ref)
>
> ["begin_cell()" in docs](/develop/func/stdlib#begin_cell)
>
> ["end_cell()" in docs](/develop/func/stdlib#end_cell)
>
> ["begin_parse()" in docs](/develop/func/stdlib#begin_parse)

### ì…€ì´ ë¹„ì–´ ìˆëŠ”ì§€ í™•ì¸í•˜ëŠ” ë°©ë²•

ì…€`ì— ë°ì´í„°ê°€ ìˆëŠ”ì§€ í™•ì¸í•˜ë ¤ë©´ ë¨¼ì € `ìŠ¬ë¼ì´ìŠ¤`ë¡œ ë³€í™˜í•´ì•¼ í•©ë‹ˆë‹¤. ë§Œì•½ `ë¹„íŠ¸`ë§Œ ìˆë‹¤ë©´ `slice_data_empty?()`ë¥¼, `ref`ë§Œ ìˆë‹¤ë©´ `slice_refs_empty?()`ë¥¼ ì‚¬ìš©í•´ì•¼ í•©ë‹ˆë‹¤. ë¹„íŠ¸`ì¸ì§€ `ref`ì¸ì§€ì— ê´€ê³„ì—†ì´ ëª¨ë“  ë°ì´í„°ì˜ ì¡´ì¬ ì—¬ë¶€ë¥¼ í™•ì¸í•˜ë ¤ë©´ `slice_empty?()`ë¥¼ ì‚¬ìš©í•´ì•¼ í•©ë‹ˆë‹¤.

```func
cell cell_with_bits_and_refs = begin_cell()
    .store_uint(1337, 16)
    .store_ref(null())
    .end_cell();

;; Change `cell` type to slice with `begin_parse()`
slice cs = cell_with_bits_and_refs.begin_parse();

;; determine if slice is empty
if (cs.slice_empty?()) {
    ;; cell is empty
}
else {
    ;; cell is not empty
}
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> ["slice_empty?()" in docs](/develop/func/stdlib#slice_empty)
>
> ["begin_cell()" in docs](/develop/func/stdlib#begin_cell)
>
> ["store_uint()" in docs](/develop/func/stdlib#store_uint)
>
> ["end_cell()" in docs](/develop/func/stdlib#end_cell)
>
> ["begin_parse()" in docs](/develop/func/stdlib#begin_parse)

### ë”•ì…”ë„ˆë¦¬ê°€ ë¹„ì–´ ìˆëŠ”ì§€ í™•ì¸í•˜ëŠ” ë°©ë²•

ë”•ì…”ë„ˆë¦¬ì— ë‚ ì§œê°€ ìˆëŠ”ì§€ í™•ì¸í•˜ëŠ” `dict_empty?()` ë©”ì„œë“œê°€ ìˆìŠµë‹ˆë‹¤. ì´ ë©”ì„œë“œëŠ” ì¼ë°˜ì ìœ¼ë¡œ `null` ì…€ì€ ë¹ˆ ë”•ì…”ë„ˆë¦¬ì´ë¯€ë¡œ `cell_null?()`ê³¼ ë™ì¼í•©ë‹ˆë‹¤.

```func
cell d = new_dict();
d~udict_set(256, 0, "hello");
d~udict_set(256, 1, "world");

if (d.dict_empty?()) { ;; Determine if dict is empty
    ;; dict is empty
}
else {
    ;; dict is not empty
}
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> ["dict_empty?()" in docs](/develop/func/stdlib#dict_empty)
>
> ["new_dict()" in docs](/develop/func/stdlib/#new_dict) ë¹ˆ ë”•ì…”ë„ˆë¦¬ë¥¼ ìƒì„±í•©ë‹ˆë‹¤.
>
> ë¬¸ì„œì˜ ["dict_set()"](/develop/func/stdlib/#dict_set)ì€ í•¨ìˆ˜ë¥¼ ì‚¬ìš©í•˜ì—¬ ë”•ì…˜ì˜ ì¼ë¶€ ìš”ì†Œë¥¼ ì¶”ê°€í•˜ë¯€ë¡œ ë¹„ì–´ ìˆì§€ ì•ŠìŠµë‹ˆë‹¤.

### íŠœí”Œì´ ë¹„ì–´ ìˆëŠ”ì§€ í™•ì¸í•˜ëŠ” ë°©ë²•

íŠœí”Œ`ë¡œ ì‘ì—…í•  ë•ŒëŠ” í•­ìƒ ë‚´ë¶€ì— ì¶”ì¶œí•  ê°’ì´ ìˆëŠ”ì§€ í™•ì¸í•˜ëŠ” ê²ƒì´ ì¤‘ìš”í•©ë‹ˆë‹¤. ë¹ˆ 'íŠœí”Œ'ì—ì„œ ê°’ì„ ì¶”ì¶œí•˜ë ¤ê³  í•˜ë©´ ì˜¤ë¥˜ê°€ ë°œìƒí•©ë‹ˆë‹¤: "ìœ íš¨í•œ í¬ê¸°ì˜ íŠœí”Œì´ ì•„ë‹™ë‹ˆë‹¤"ë¼ëŠ” ì˜¤ë¥˜ì™€ í•¨ê»˜ `ì¢…ë£Œ ì½”ë“œ 7\`ì´ í‘œì‹œë©ë‹ˆë‹¤.

```func
;; Declare tlen function because it's not presented in stdlib
(int) tlen (tuple t) asm "TLEN";

() main () {
    tuple t = empty_tuple();
    t~tpush(13);
    t~tpush(37);

    if (t.tlen() == 0) {
        ;; tuple is empty
    }
    else {
        ;; tuple is not empty
    }
}
```

> ğŸ’¡ ì°¸ê³ 
>
> tlen ì–´ì…ˆë¸”ë¦¬ í•¨ìˆ˜ë¥¼ ì„ ì–¸í•©ë‹ˆë‹¤. ìì„¸í•œ ë‚´ìš©ì€ [ì—¬ê¸°](/ê°œë°œ/í€í¬/í•¨ìˆ˜#ì–´ì…ˆë¸”ëŸ¬-í•¨ìˆ˜-ë³¸ë¬¸-ì •ì˜)ì—ì„œ í™•ì¸í•  ìˆ˜ ìˆìœ¼ë©° [ëª¨ë“  ì–´ì…ˆë¸”ëŸ¬ ëª…ë ¹ì–´ ëª©ë¡](/í•™ìŠµ/tvm-ì§€ì¹¨/ì§€ì¹¨)ì„ ì°¸ì¡°í•˜ì„¸ìš”.

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> ["empty_tuple?()" in docs](/develop/func/stdlib#empty_tuple)
>
> ["tpush()" in docs](/develop/func/stdlib/#tpush)
>
> [ë¬¸ì„œì˜ "ì¢…ë£Œ ì½”ë“œ"](/í•™ìŠµ/tvm-ì§€ì¹¨/tvm-exit-codes)

### ë¦¬ìŠ¤í”„ ìŠ¤íƒ€ì¼ ëª©ë¡ì´ ë¹„ì–´ ìˆëŠ”ì§€ í™•ì¸í•˜ëŠ” ë°©ë²•

```func
tuple numbers = null();
numbers = cons(100, numbers);

if (numbers.null?()) {
    ;; list-style list is empty
} else {
    ;; list-style list is not empty
}
```

ë‹¨ì ](/develop/func/stdlib/#cons) í•¨ìˆ˜ë¥¼ ì‚¬ìš©í•˜ì—¬ ëª©ë¡ ìŠ¤íƒ€ì¼ ëª©ë¡ì— 100ë²ˆì„ ì¶”ê°€í•˜ê³  ìˆìœ¼ë¯€ë¡œ ë¹„ì–´ ìˆì§€ ì•ŠìŠµë‹ˆë‹¤.

### ì»¨íŠ¸ë™íŠ¸ê°€ ë¹„ì–´ ìˆëŠ” ìƒíƒœë¥¼ í™•ì¸í•˜ëŠ” ë°©ë²•

íŠ¸ëœì­ì…˜ ìˆ˜ë¥¼ ì €ì¥í•˜ëŠ” 'ì¹´ìš´í„°'ê°€ ìˆë‹¤ê³  ê°€ì •í•´ ë´…ì‹œë‹¤. ì´ ë³€ìˆ˜ëŠ” ìŠ¤ë§ˆíŠ¸ ì»¨íŠ¸ë™íŠ¸ ìƒíƒœì˜ ì²« ë²ˆì§¸ íŠ¸ëœì­ì…˜ì—ì„œëŠ” ìƒíƒœê°€ ë¹„ì–´ ìˆê¸° ë•Œë¬¸ì— ì‚¬ìš©í•  ìˆ˜ ì—†ìœ¼ë¯€ë¡œ ì´ëŸ¬í•œ ê²½ìš°ë¥¼ ì²˜ë¦¬í•  í•„ìš”ê°€ ìˆìŠµë‹ˆë‹¤. ìƒíƒœê°€ ë¹„ì–´ ìˆìœ¼ë©´ 'ì¹´ìš´í„°'ë¼ëŠ” ë³€ìˆ˜ë¥¼ ìƒì„±í•˜ê³  ì €ì¥í•©ë‹ˆë‹¤.

```func
;; `get_data()` will return the data cell from contract state
cell contract_data = get_data();
slice cs = contract_data.begin_parse();

if (cs.slice_empty?()) {
    ;; contract data is empty, so we create counter and save it
    int counter = 1;
    ;; create cell, add counter and save in contract state
    set_data(begin_cell().store_uint(counter, 32).end_cell());
}
else {
    ;; contract data is not empty, so we get our counter, increase it and save
    ;; we should specify correct length of our counter in bits
    int counter = cs~load_uint(32) + 1;
    set_data(begin_cell().store_uint(counter, 32).end_cell());
}
```

> ğŸ’¡ ì°¸ê³ 
>
> ì…€ì´ ë¹„ì–´ ìˆëŠ”ì§€ í™•ì¸í•˜ë ¤ë©´ [ì…€ì´ ë¹„ì–´ ìˆëŠ”ì§€](/develop/func/cookbook#how-to-determine-if-cell-is-empty)ë¥¼ í™•ì¸í•˜ì—¬ ì»¨íŠ¸ë™íŠ¸ ìƒíƒœê°€ ë¹„ì–´ ìˆëŠ”ì§€ í™•ì¸í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> ["get_data()" in docs](/develop/func/stdlib#get_data)
>
> ["begin_parse()" in docs](/develop/func/stdlib/#begin_parse)
>
> ["slice_empty?()" in docs](/develop/func/stdlib/#slice_empty)
>
> ["set_data?()" in docs](/develop/func/stdlib#set_data)

### ë‚´ë¶€ ë©”ì‹œì§€ ì…€ì„ ë§Œë“œëŠ” ë°©ë²•

ì»¨íŠ¸ë™íŠ¸ê°€ ë‚´ë¶€ ë©”ì‹œì§€ë¥¼ ë³´ë‚´ë ¤ë©´ ë¨¼ì € ê¸°ìˆ  í”Œë˜ê·¸, ìˆ˜ì‹ ì ì£¼ì†Œ, ë‚˜ë¨¸ì§€ ë°ì´í„°ë¥¼ ì§€ì •í•˜ì—¬ ì…€ë¡œ ì˜¬ë°”ë¥´ê²Œ ìƒì„±í•´ì•¼ í•©ë‹ˆë‹¤.

```func
;; We use literal `a` to get valid address inside slice from string containing address 
slice addr = "EQArzP5prfRJtDM5WrMNWyr9yUTAi0c9o6PfR4hkWy9UQXHx"a;
int amount = 1000000000;
;; we use `op` for identifying operations
int op = 0;

cell msg = begin_cell()
    .store_uint(0x18, 6)
    .store_slice(addr)
    .store_coins(amount)
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; default message headers (see sending messages page)
    .store_uint(op, 32)
.end_cell();

send_raw_message(msg, 3); ;; mode 3 - pay fees separately and ignore errors 
```

> ğŸ’¡ ì°¸ê³ 
>
> ì´ ì˜ˆì œì—ì„œëŠ” ë¦¬í„°ëŸ´ `a`ë¥¼ ì‚¬ìš©í•˜ì—¬ ì£¼ì†Œë¥¼ ê°€ì ¸ì˜µë‹ˆë‹¤. ë¬¸ìì—´ ë¦¬í„°ëŸ´ì— ëŒ€í•œ ìì„¸í•œ ë‚´ìš©ì€ [ë¬¸ì„œ](/ê°œë°œ/í€í¬/ë¦¬í„°ëŸ´_ì‹ë³„ì#string-literals)ì—ì„œ í™•ì¸í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.

> ğŸ’¡ ì°¸ê³ 
>
> ìì„¸í•œ ë‚´ìš©ì€ [ë¬¸ì„œ](/ê°œë°œ/ìŠ¤ë§ˆíŠ¸ê³„ì•½/ë©”ì‹œì§€)ì—ì„œ í™•ì¸í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤. ë˜í•œ ì´ ë§í¬ë¥¼ í†µí•´ [ë ˆì´ì•„ì›ƒ](/ê°œë°œ/ìŠ¤ë§ˆíŠ¸ê³„ì•½/ë©”ì‹œì§€#ë©”ì‹œì§€-ë ˆì´ì•„ì›ƒ)ìœ¼ë¡œ ì´ë™í•  ìˆ˜ë„ ìˆìŠµë‹ˆë‹¤.

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> ["begin_cell()" in docs](/develop/func/stdlib#begin_cell)
>
> ["store_uint()" in docs](/develop/func/stdlib#store_uint)
>
> ["store_slice()" in docs](/develop/func/stdlib#store_slice)
>
> ["store_coins()" in docs](/develop/func/stdlib#store_coins)
>
> ["end_cell()" in docs](/develop/func/stdlib/#end_cell)
>
> ["send_raw_message()" in docs](/develop/func/stdlib/#send_raw_message)

### ë‚´ë¶€ ë©”ì‹œì§€ ì…€ì— ì°¸ì¡°ë¡œ ë³¸ë¬¸ì„ í¬í•¨í•˜ëŠ” ë°©ë²•

í”Œë˜ê·¸ ë° ê¸°íƒ€ ê¸°ìˆ  ë°ì´í„° ë’¤ì— ì˜¤ëŠ” ë©”ì‹œì§€ ë³¸ë¬¸ì—ëŠ” `int`, `ìŠ¬ë¼ì´ìŠ¤`, `ì…€`ì„ ë³´ë‚¼ ìˆ˜ ìˆìŠµë‹ˆë‹¤. í›„ìì˜ ê²½ìš° `store_ref()` ì•ì— ë¹„íŠ¸ë¥¼ `1`ë¡œ ì„¤ì •í•˜ì—¬ `ì…€`ì´ ê³„ì† ì§„í–‰ë¨ì„ í‘œì‹œí•´ì•¼ í•©ë‹ˆë‹¤.

ê³µê°„ì´ ì¶©ë¶„í•˜ë‹¤ê³  í™•ì‹ í•˜ëŠ” ê²½ìš° í—¤ë”ì™€ ë™ì¼í•œ 'ì…€' ì•ˆì— ë©”ì‹œì§€ ë³¸ë¬¸ì„ ë³´ë‚¼ ìˆ˜ë„ ìˆìŠµë‹ˆë‹¤. ì´ ê²½ìš° ë¹„íŠ¸ë¥¼ `0`ìœ¼ë¡œ ì„¤ì •í•´ì•¼ í•©ë‹ˆë‹¤.

```func
;; We use literal `a` to get valid address inside slice from string containing address 
slice addr = "EQArzP5prfRJtDM5WrMNWyr9yUTAi0c9o6PfR4hkWy9UQXHx"a;
int amount = 1000000000;
int op = 0;
cell message_body = begin_cell() ;; Creating a cell with message
    .store_uint(op, 32)
    .store_slice("â¤")
.end_cell();
    
cell msg = begin_cell()
    .store_uint(0x18, 6)
    .store_slice(addr)
    .store_coins(amount)
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1) ;; default message headers (see sending messages page)
    .store_uint(1, 1) ;; set bit to 1 to indicate that the cell will go on
    .store_ref(message_body)
.end_cell();

send_raw_message(msg, 3); ;; mode 3 - pay fees separately and ignore errors 
```

> ğŸ’¡ ì°¸ê³ 
>
> ì´ ì˜ˆì œì—ì„œëŠ” ë¦¬í„°ëŸ´ `a`ë¥¼ ì‚¬ìš©í•˜ì—¬ ì£¼ì†Œë¥¼ ê°€ì ¸ì˜µë‹ˆë‹¤. ë¬¸ìì—´ ë¦¬í„°ëŸ´ì— ëŒ€í•œ ìì„¸í•œ ë‚´ìš©ì€ [ë¬¸ì„œ](/ê°œë°œ/í€í¬/ë¦¬í„°ëŸ´_ì‹ë³„ì#string-literals)ì—ì„œ í™•ì¸í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.

> ğŸ’¡ ì°¸ê³ 
>
> ì´ ì˜ˆì—ì„œëŠ” ëª¨ë“œ 3ì„ ì‚¬ìš©í•˜ì—¬ ë“¤ì–´ì˜¤ëŠ” í†¤ì„ ê°€ì ¸ì™€ì„œ ê³„ì•½ ì”ì•¡ì—ì„œ ìˆ˜ìˆ˜ë£Œë¥¼ ì§€ë¶ˆí•˜ê³  ì˜¤ë¥˜ë¥¼ ë¬´ì‹œí•œ ì±„ ì§€ì •ëœ ì–‘(ê¸ˆì•¡)ì„ ì •í™•íˆ ì „ì†¡í–ˆìŠµë‹ˆë‹¤. ë°›ì€ ëª¨ë“  í†¤ì—ì„œ ìˆ˜ìˆ˜ë£Œë¥¼ ëº€ ê¸ˆì•¡ì„ ë°˜í™˜í•˜ë ¤ë©´ ëª¨ë“œ 64ê°€ í•„ìš”í•˜ë©°, ëª¨ë“œ 128ì€ ì „ì²´ ì”ì•¡ì„ ì „ì†¡í•©ë‹ˆë‹¤.

> ğŸ’¡ ì°¸ê³ 
>
> ë©”ì‹œì§€ ì‘ì„±](/develop/func/cookbook#how-to-build-an-internal-message-cell)ì„ í•˜ê³  ìˆì§€ë§Œ ë©”ì‹œì§€ ë³¸ë¬¸ì€ ë³„ë„ë¡œ ì¶”ê°€í•˜ê³  ìˆìŠµë‹ˆë‹¤.

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> ["begin_cell()" in docs](/develop/func/stdlib#begin_cell)
>
> ["store_uint()" in docs](/develop/func/stdlib#store_uint)
>
> ["store_slice()" in docs](/develop/func/stdlib#store_slice)
>
> ["store_coins()" in docs](/develop/func/stdlib#store_coins)
>
> ["end_cell()" in docs](/develop/func/stdlib/#end_cell)
>
> ["send_raw_message()" in docs](/develop/func/stdlib/#send_raw_message)

### ë‚´ë¶€ ë©”ì‹œì§€ ì…€ì— ìŠ¬ë¼ì´ìŠ¤ë¡œ ë³¸ë¬¸ì„ í¬í•¨í•˜ëŠ” ë°©ë²•

ë©”ì‹œì§€ë¥¼ ë³´ë‚¼ ë•Œ ë³¸ë¬¸ ë©”ì‹œì§€ëŠ” `ì…€` ë˜ëŠ” `ìŠ¬ë¼ì´ìŠ¤`ë¡œ ë³´ë‚¼ ìˆ˜ ìˆìŠµë‹ˆë‹¤. ì´ ì˜ˆì—ì„œëŠ” `ìŠ¬ë¼ì´ìŠ¤` ì•ˆì— ë©”ì‹œì§€ ë³¸ë¬¸ì„ ë³´ëƒ…ë‹ˆë‹¤.

```func
;; We use literal `a` to get valid address inside slice from string containing address 
slice addr = "EQArzP5prfRJtDM5WrMNWyr9yUTAi0c9o6PfR4hkWy9UQXHx"a;
int amount = 1000000000;
int op = 0;
slice message_body = "â¤"; 

cell msg = begin_cell()
    .store_uint(0x18, 6)
    .store_slice(addr)
    .store_coins(amount)
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; default message headers (see sending messages page)
    .store_uint(op, 32)
    .store_slice(message_body)
.end_cell();

send_raw_message(msg, 3); ;; mode 3 - pay fees separately and ignore errors 
```

> ğŸ’¡ ì°¸ê³ 
>
> ì´ ì˜ˆì œì—ì„œëŠ” ë¦¬í„°ëŸ´ `a`ë¥¼ ì‚¬ìš©í•˜ì—¬ ì£¼ì†Œë¥¼ ê°€ì ¸ì˜µë‹ˆë‹¤. ë¬¸ìì—´ ë¦¬í„°ëŸ´ì— ëŒ€í•œ ìì„¸í•œ ë‚´ìš©ì€ [ë¬¸ì„œ](/ê°œë°œ/í€í¬/ë¦¬í„°ëŸ´_ì‹ë³„ì#string-literals)ì—ì„œ í™•ì¸í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.

> ğŸ’¡ ì°¸ê³ 
>
> ì´ ì˜ˆì—ì„œëŠ” ëª¨ë“œ 3ì„ ì‚¬ìš©í•˜ì—¬ ë“¤ì–´ì˜¤ëŠ” í†¤ì„ ê°€ì ¸ì™€ì„œ ê³„ì•½ ì”ì•¡ì—ì„œ ìˆ˜ìˆ˜ë£Œë¥¼ ì§€ë¶ˆí•˜ê³  ì˜¤ë¥˜ë¥¼ ë¬´ì‹œí•œ ì±„ ì§€ì •ëœ ì–‘(ê¸ˆì•¡)ì„ ì •í™•íˆ ì „ì†¡í–ˆìŠµë‹ˆë‹¤. ë°›ì€ ëª¨ë“  í†¤ì—ì„œ ìˆ˜ìˆ˜ë£Œë¥¼ ëº€ ê¸ˆì•¡ì„ ë°˜í™˜í•˜ë ¤ë©´ ëª¨ë“œ 64ê°€ í•„ìš”í•˜ë©°, ëª¨ë“œ 128ì€ ì „ì²´ ì”ì•¡ì„ ì „ì†¡í•©ë‹ˆë‹¤.

> ğŸ’¡ ì°¸ê³ 
>
> ìš°ë¦¬ëŠ” [ë©”ì‹œì§€ êµ¬ì¶•](/develop/func/cookbook#how-to-build-an-internal-message-cell)ì„ í•˜ê³  ìˆì§€ë§Œ ë©”ì‹œì§€ë¥¼ ìŠ¬ë¼ì´ìŠ¤ë¡œ ì¶”ê°€í•˜ê³  ìˆìŠµë‹ˆë‹¤.

### íŠœí”Œì„ ë°˜ë³µí•˜ëŠ” ë°©ë²•(ì–‘ë°©í–¥)

FunCì—ì„œ ë°°ì—´ì´ë‚˜ ìŠ¤íƒìœ¼ë¡œ ì‘ì—…í•˜ë ¤ë©´ íŠœí”Œì´ í•„ìš”í•©ë‹ˆë‹¤. ê·¸ë¦¬ê³  ìš°ì„  ê°’ì„ ë°˜ë³µí•˜ì—¬ ì‘ì—…í•  ìˆ˜ ìˆì–´ì•¼ í•©ë‹ˆë‹¤.

```func
(int) tlen (tuple t) asm "TLEN";
forall X -> (tuple) to_tuple (X x) asm "NOP";

() main () {
    tuple t = to_tuple([1, 2, 3, 4, 5, 6, 7, 8, 9, 10]);
    int len = t.tlen();
    
    int i = 0;
    while (i < len) {
        int x = t.at(i);
        ;; do something with x
        i = i + 1;
    }

    i = len - 1;
    while (i >= 0) {
        int x = t.at(i);
        ;; do something with x
        i = i - 1;
    }
}
```

> ğŸ’¡ ì°¸ê³ 
>
> tlen\` ì–´ì…ˆë¸”ë¦¬ í•¨ìˆ˜ë¥¼ ì„ ì–¸í•©ë‹ˆë‹¤. ìì„¸í•œ ë‚´ìš©ì€ [ì—¬ê¸°](/ê°œë°œ/í€í¬/í•¨ìˆ˜#ì–´ì…ˆë¸”ëŸ¬-í•¨ìˆ˜-ë³¸ë¬¸-ì •ì˜)ì—ì„œ í™•ì¸í•  ìˆ˜ ìˆìœ¼ë©°, [ëª¨ë“  ì–´ì…ˆë¸”ëŸ¬ ëª…ë ¹ì–´ ëª©ë¡](/í•™ìŠµ/tvm-ì§€ì¹¨/ì§€ì¹¨)ì„ ì°¸ì¡°í•˜ì„¸ìš”.
>
> ë˜í•œ `to_tuple` í•¨ìˆ˜ë¥¼ ì„ ì–¸í•©ë‹ˆë‹¤. ì´ í•¨ìˆ˜ëŠ” ëª¨ë“  ì…ë ¥ì˜ ë°ì´í„° ìœ í˜•ì„ íŠœí”Œë¡œ ë³€ê²½í•˜ë¯€ë¡œ ì‚¬ìš©í•  ë•Œ ì£¼ì˜í•´ì•¼ í•©ë‹ˆë‹¤.

### ì•„ìŠ¤\` í‚¤ì›Œë“œë¥¼ ì‚¬ìš©í•˜ì—¬ ìì²´ í•¨ìˆ˜ë¥¼ ì‘ì„±í•˜ëŠ” ë°©ë²•

ì–´ë–¤ ê¸°ëŠ¥ì„ ì‚¬ìš©í•  ë•ŒëŠ” ì‹¤ì œë¡œ `stdlib.fc`ì— ë¯¸ë¦¬ ì¤€ë¹„ëœ ë©”ì„œë“œë¥¼ ì‚¬ìš©í•©ë‹ˆë‹¤. í•˜ì§€ë§Œ ì‹¤ì œë¡œëŠ” ë” ë§ì€ ê¸°íšŒë¥¼ ì‚¬ìš©í•  ìˆ˜ ìˆìœ¼ë©° ì§ì ‘ ì‘ì„±í•˜ëŠ” ë°©ë²•ì„ ë°°ì›Œì•¼ í•©ë‹ˆë‹¤.

ì˜ˆë¥¼ ë“¤ì–´, 'tpush'ë¼ëŠ” ë©”ì„œë“œëŠ” 'tuple'ì— ìš”ì†Œë¥¼ ì¶”ê°€í•˜ì§€ë§Œ 'tpop'ì´ ì—†ëŠ” ë©”ì„œë“œì…ë‹ˆë‹¤. ì´ ê²½ìš°ì—ëŠ” ì´ë ‡ê²Œ í•´ì•¼ í•©ë‹ˆë‹¤:

```func
;; ~ means it is modifying method
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP"; 
```

ë°˜ë³µì„ ìœ„í•œ 'íŠœí”Œ'ì˜ ê¸¸ì´ë¥¼ ì•Œê³  ì‹¶ë‹¤ë©´ `TLEN` asm ëª…ë ¹ì–´ë¥¼ ì‚¬ìš©í•˜ì—¬ ìƒˆ í•¨ìˆ˜ë¥¼ ì‘ì„±í•´ì•¼ í•©ë‹ˆë‹¤:

```func
int tuple_length (tuple t) asm "TLEN";
```

stdlib.fcì—ì„œ ì´ë¯¸ ì•Œë ¤ì§„ í•¨ìˆ˜ì˜ ëª‡ ê°€ì§€ ì˜ˆì…ë‹ˆë‹¤:

```func
slice begin_parse(cell c) asm "CTOS";
builder begin_cell() asm "NEWC";
cell end_cell(builder b) asm "ENDC";
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬:
>
> [ë¬¸ì„œì˜ "ìˆ˜ì • ë°©ë²•"](/ê°œë°œ/í€í¬/ìŠ¤í…Œì´íŠ¸ë¨¼íŠ¸#ìˆ˜ì • ë°©ë²•)
>
> [ë¬¸ì„œì—ì„œ "stdlib"](/ê°œë°œ/í€í¬/stdlib)
>
> [ë¬¸ì„œì˜ 'TVM ì§€ì¹¨'](/í•™ìŠµ/tvm-ì§€ì¹¨/ì§€ì¹¨)

### n ì¤‘ì²© íŠœí”Œ ë°˜ë³µí•˜ê¸°

ì¤‘ì²©ëœ íŠœí”Œì„ ë°˜ë³µí•˜ê³  ì‹¶ì„ ë•Œê°€ ìˆìŠµë‹ˆë‹¤. ë‹¤ìŒ ì˜ˆì œëŠ” `[[2,6],[1,[3,[3,5]], 3]` í˜•ì‹ì˜ íŠœí”Œì— ìˆëŠ” ëª¨ë“  í•­ëª©ì„ ë¨¸ë¦¬ë¶€í„° ë°˜ë³µí•˜ì—¬ ì¶œë ¥í•©ë‹ˆë‹¤.

```func
int tuple_length (tuple t) asm "TLEN";
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";
forall X -> int is_tuple (X x) asm "ISTUPLE";
forall X -> tuple cast_to_tuple (X x) asm "NOP";
forall X -> int cast_to_int (X x) asm "NOP";
forall X -> (tuple) to_tuple (X x) asm "NOP";

;; define global variable
global int max_value;

() iterate_tuple (tuple t) impure {
    repeat (t.tuple_length()) {
        var value = t~tpop();
        if (is_tuple(value)) {
            tuple tuple_value = cast_to_tuple(value);
            iterate_tuple(tuple_value);
        }
        else {
            if(value > max_value) {
                max_value = value;
            }
        }
    }
}

() main () {
    tuple t = to_tuple([[2,6], [1, [3, [3, 5]]], 3]);
    int len = t.tuple_length();
    max_value = 0; ;; reset max_value;
    iterate_tuple(t); ;; iterate tuple and find max value
    ~dump(max_value); ;; 6
}
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> [ë¬¸ì„œì˜ "ì „ì—­ ë³€ìˆ˜"](/ê°œë°œ/í€í¬/ê¸€ë¡œë²Œ_ë³€ìˆ˜)
>
> [ë¬¸ì„œì—ì„œ "~ë¤í”„"](/ê°œë°œ/í€í¬/ë¹ŒíŠ¸ì¸#ë¤í”„-ë³€ìˆ˜)
>
> [ë¬¸ì„œì˜ 'TVM ì§€ì¹¨'](/í•™ìŠµ/tvm-ì§€ì¹¨/ì§€ì¹¨)

### íŠœí”Œì„ ì‚¬ìš©í•œ ê¸°ë³¸ ì—°ì‚°

```func
(int) tlen (tuple t) asm "TLEN";
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";

() main () {
    ;; creating an empty tuple
    tuple names = empty_tuple(); 
    
    ;; push new items
    names~tpush("Naito Narihira");
    names~tpush("Shiraki Shinichi");
    names~tpush("Akamatsu Hachemon");
    names~tpush("Takaki Yuichi");
    
    ;; pop last item
    slice last_name = names~tpop();

    ;; get first item
    slice first_name = names.first();

    ;; get an item by index
    slice best_name = names.at(2);

    ;; getting the length of the list 
    int number_names = names.tlen();
}
```

### X ìœ í˜• í•´ê²°

ë‹¤ìŒ ì˜ˆì œëŠ” ì–´ë–¤ ê°’ì´ íŠœí”Œì— í¬í•¨ë˜ì–´ ìˆëŠ”ë° íŠœí”Œì— ê°’ X(ì…€, ìŠ¬ë¼ì´ìŠ¤, int, íŠœí”Œ, int)ê°€ í¬í•¨ë˜ì–´ ìˆëŠ”ì§€ í™•ì¸í•˜ëŠ” ì˜ˆì œì…ë‹ˆë‹¤. ê°’ì„ í™•ì¸í•˜ê³  ê·¸ì— ë”°ë¼ í˜•ë³€í™˜í•´ì•¼ í•©ë‹ˆë‹¤.

```func
forall X -> int is_null (X x) asm "ISNULL";
forall X -> int is_int (X x) asm "<{ TRY:<{ 0 PUSHINT ADD DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_cell (X x) asm "<{ TRY:<{ CTOS DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_slice (X x) asm "<{ TRY:<{ SBITS DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_tuple (X x) asm "ISTUPLE";
forall X -> int cast_to_int (X x) asm "NOP";
forall X -> cell cast_to_cell (X x) asm "NOP";
forall X -> slice cast_to_slice (X x) asm "NOP";
forall X -> tuple cast_to_tuple (X x) asm "NOP";
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";

forall X -> () resolve_type (X value) impure {
    ;; value here is of type X, since we dont know what is the exact value - we would need to check what is the value and then cast it
    
    if (is_null(value)) {
        ;; do something with the null
    }
    elseif (is_int(value)) {
        int valueAsInt = cast_to_int(value);
        ;; do something with the int
    }
    elseif (is_slice(value)) {
        slice valueAsSlice = cast_to_slice(value);
        ;; do something with the slice
    }
    elseif (is_cell(value)) {
        cell valueAsCell = cast_to_cell(value);
        ;; do something with the cell
    }
    elseif (is_tuple(value)) {
        tuple valueAsTuple = cast_to_tuple(value);
        ;; do something with the tuple
    }
}

() main () {
    ;; creating an empty tuple
    tuple stack = empty_tuple();
    ;; let's say we have tuple and do not know the exact types of them
    stack~tpush("Some text");
    stack~tpush(4);
    ;; we use var because we do not know type of value
    var value = stack~tpop();
    resolve_type(value);
}
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> [ë¬¸ì„œì˜ 'TVM ì§€ì¹¨'](/í•™ìŠµ/tvm-ì§€ì¹¨/ì§€ì¹¨)

### í˜„ì¬ ì‹œê°„ì„ ì–»ëŠ” ë°©ë²•

```func
int current_time = now();
  
if (current_time > 1672080143) {
    ;; do some stuff 
}
```

### ë‚œìˆ˜ ìƒì„± ë°©ë²•

:::caution ì´ˆì•ˆ

ìì„¸í•œ ë‚´ìš©ì€ [ë‚œìˆ˜ ìƒì„±](https://docs.ton.org/develop/smart-contracts/guidelines/random-number-generation)ì„ í™•ì¸í•˜ì„¸ìš”.
:::

```func
randomize_lt(); ;; do this once

int a = rand(10);
int b = rand(1000000);
int c = random();
```

### ëª¨ë“ˆì‹ ì—°ì‚°

ì˜ˆë¥¼ ë“¤ì–´ ëª¨ë“  256ê°œì˜ ìˆ«ìì— ëŒ€í•´ `(xp + zp)*(xp-zp)`ë¼ëŠ” ì—°ì‚°ì„ ì‹¤í–‰í•œë‹¤ê³  ê°€ì •í•´ ë³´ê² ìŠµë‹ˆë‹¤. ì´ëŸ¬í•œ ì—°ì‚°ì€ ëŒ€ë¶€ë¶„ ì•”í˜¸í™”ì— ì‚¬ìš©ë˜ë¯€ë¡œ ë‹¤ìŒ ì˜ˆì œì—ì„œëŠ” ëª¨ë“ˆë¡œ ì—°ì‚°ìë¥¼ ëª¬í† ê³ ë©”ë¦¬ ê³¡ì„ ì— ì‚¬ìš©í•©ë‹ˆë‹¤.
xp+zpëŠ” ìœ íš¨í•œ ë³€ìˆ˜ ì´ë¦„(ê³µë°± ì—†ì´)ì´ë¼ëŠ” ì ì— ìœ ì˜í•˜ì„¸ìš”.

```func
(int) modulo_operations (int xp, int zp) {  
   ;; 2^255 - 19 is a prime number for montgomery curves, meaning all operations should be done against its prime
   int prime = 57896044618658097711785492504343953926634992332820282019728792003956564819949; 

   ;; muldivmod handles the next two lines itself
   ;; int xp+zp = (xp + zp) % prime;
   ;; int xp-zp = (xp - zp + prime) % prime;
   (_, int xp+zp*xp-zp) = muldivmod(xp + zp, xp - zp, prime);
   return xp+zp*xp-zp;
}
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> [ë¬¸ì„œì˜ "muldivmod"](/í•™ìŠµ/tvm-ì§€ì¹¨/ì§€ì¹¨#52-division)

### ì˜¤ë¥˜ë¥¼ ë˜ì§€ëŠ” ë°©ë²•

```func
int number = 198;

throw_if(35, number > 50); ;; the error will be triggered only if the number is greater than 50

throw_unless(39, number == 198); ;; the error will be triggered only if the number is NOT EQUAL to 198

throw(36); ;; the error will be triggered anyway
```

[í‘œì¤€ tvm ì˜ˆì™¸ ì½”ë“œ](/learn/tvm-instructions/tvm-exit-codes.md)

### íŠœí”Œ ë°˜ì „

íŠœí”Œì€ ë°ì´í„°ë¥¼ ìŠ¤íƒìœ¼ë¡œ ì €ì¥í•˜ê¸° ë•Œë¬¸ì— ë•Œë¡œëŠ” ë°˜ëŒ€ìª½ì—ì„œ ë°ì´í„°ë¥¼ ì½ê¸° ìœ„í•´ íŠœí”Œì„ ì—­ë°©í–¥ìœ¼ë¡œ ë°”ê¿”ì•¼ í•  ë•Œê°€ ìˆìŠµë‹ˆë‹¤.

```func
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";
int tuple_length (tuple t) asm "TLEN";
forall X -> (tuple) to_tuple (X x) asm "NOP";

(tuple) reverse_tuple (tuple t1) {
    tuple t2 = empty_tuple();
    repeat (t1.tuple_length()) {
        var value = t1~tpop();
        t2~tpush(value);
    }
    return t2;
}

() main () {
    tuple t = to_tuple([1, 2, 3, 4, 5, 6, 7, 8, 9, 10]);
    tuple reversed_t = reverse_tuple(t);
    ~dump(reversed_t); ;; [10 9 8 7 6 5 4 3 2 1]
}
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> ["tpush()" in docs](/develop/func/stdlib/#tpush)

### ëª©ë¡ì—ì„œ íŠ¹ì • ìƒ‰ì¸ì´ ìˆëŠ” í•­ëª©ì„ ì‚­ì œí•˜ëŠ” ë°©ë²•

```func
int tlen (tuple t) asm "TLEN";

(tuple, ()) remove_item (tuple old_tuple, int place) {
    tuple new_tuple = empty_tuple();

    int i = 0;
    while (i < old_tuple.tlen()) {
        int el = old_tuple.at(i);
        if (i != place) {
            new_tuple~tpush(el);
        }
        i += 1;  
    }
    return (new_tuple, ());
}

() main () {
    tuple numbers = empty_tuple();

    numbers~tpush(19);
    numbers~tpush(999);
    numbers~tpush(54);

    ~dump(numbers); ;; [19 999 54]

    numbers~remove_item(1); 

    ~dump(numbers); ;; [19 54]
}
```

### ìŠ¬ë¼ì´ìŠ¤ê°€ ë™ì¼í•œì§€ í™•ì¸

ë™ë“±ì„±ì„ íŒë‹¨í•˜ëŠ” ë°©ë²•ì—ëŠ” ë‘ ê°€ì§€ê°€ ìˆìŠµë‹ˆë‹¤. í•˜ë‚˜ëŠ” ìŠ¬ë¼ì´ìŠ¤ í•´ì‹œë¥¼ ê¸°ë°˜ìœ¼ë¡œ í•˜ëŠ” ê²ƒì´ê³ , ë‹¤ë¥¸ í•˜ë‚˜ëŠ” SDEQ asm ëª…ë ¹ì–´ë¥¼ ì‚¬ìš©í•˜ëŠ” ê²ƒì…ë‹ˆë‹¤.

```func
int are_slices_equal_1? (slice a, slice b) {
    return a.slice_hash() == b.slice_hash();
}

int are_slices_equal_2? (slice a, slice b) asm "SDEQ";

() main () {
    slice a = "Some text";
    slice b = "Some text";
    ~dump(are_slices_equal_1?(a, b)); ;; -1 = true

    a = "Text";
    ;; We use literal `a` to get valid address inside slice from string containing address
    b = "EQDKbjIcfM6ezt8KjKJJLshZJJSqX7XOA4ff-W72r5gqPrHF"a;
    ~dump(are_slices_equal_2?(a, b)); ;; 0 = false
}
```

#### ğŸ’¡ ìœ ìš©í•œ ë§í¬

- ["slice_hash()" in docs](/develop/func/stdlib/#slice_hash)
- [ë¬¸ì„œì˜ "SDEQ"](/í•™ìŠµ/tvm-ì§€ì¹¨/ì§€ì¹¨#62-ë‹¤ë¥¸ ë¹„êµ)

### ì…€ì´ ë™ì¼í•œì§€ í™•ì¸

í•´ì‹œë¥¼ ê¸°ë°˜ìœ¼ë¡œ ì…€ì˜ ë™ì¼ì„±ì„ ì‰½ê²Œ í™•ì¸í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.

```func
int are_cells_equal? (cell a, cell b) {
    return a.cell_hash() == b.cell_hash();
}

() main () {
    cell a = begin_cell()
            .store_uint(123, 16)
            .end_cell();

    cell b = begin_cell()
            .store_uint(123, 16)
            .end_cell();

    ~dump(are_cells_equal?(a, b)); ;; -1 = true
}
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> ["cell_hash()" in docs](/develop/func/stdlib/#cell_hash)

### íŠœí”Œì´ ë™ì¼í•œì§€ í™•ì¸

ì¢€ ë” ê³ ê¸‰ ì˜ˆëŠ” ê° íŠœí”Œ ê°’ì„ ë°˜ë³µí•˜ê³  ë¹„êµí•˜ëŠ” ê²ƒì…ë‹ˆë‹¤. Xì´ë¯€ë¡œ í•´ë‹¹ ìœ í˜•ì„ í™•ì¸í•˜ê³  í•´ë‹¹ ìœ í˜•ìœ¼ë¡œ í˜•ë³€í™˜í•´ì•¼ í•˜ë©°, íŠœí”Œì¸ ê²½ìš° ì¬ê·€ì ìœ¼ë¡œ ë°˜ë³µí•´ì•¼ í•©ë‹ˆë‹¤.

```func
int tuple_length (tuple t) asm "TLEN";
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";
forall X -> int cast_to_int (X x) asm "NOP";
forall X -> cell cast_to_cell (X x) asm "NOP";
forall X -> slice cast_to_slice (X x) asm "NOP";
forall X -> tuple cast_to_tuple (X x) asm "NOP";
forall X -> int is_null (X x) asm "ISNULL";
forall X -> int is_int (X x) asm "<{ TRY:<{ 0 PUSHINT ADD DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_cell (X x) asm "<{ TRY:<{ CTOS DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_slice (X x) asm "<{ TRY:<{ SBITS DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_tuple (X x) asm "ISTUPLE";
int are_slices_equal? (slice a, slice b) asm "SDEQ";

int are_cells_equal? (cell a, cell b) {
    return a.cell_hash() == b.cell_hash();
}

(int) are_tuples_equal? (tuple t1, tuple t2) {
    int equal? = -1; ;; initial value to true
    
    if (t1.tuple_length() != t2.tuple_length()) {
        ;; if tuples are differ in length they cannot be equal
        return 0;
    }

    int i = t1.tuple_length();
    
    while (i > 0 & equal?) {
        var v1 = t1~tpop();
        var v2 = t2~tpop();
        
        if (is_null(t1) & is_null(t2)) {
            ;; nulls are always equal
        }
        elseif (is_int(v1) & is_int(v2)) {
            if (cast_to_int(v1) != cast_to_int(v2)) {
                equal? = 0;
            }
        }
        elseif (is_slice(v1) & is_slice(v2)) {
            if (~ are_slices_equal?(cast_to_slice(v1), cast_to_slice(v2))) {
                equal? = 0;
            }
        }
        elseif (is_cell(v1) & is_cell(v2)) {
            if (~ are_cells_equal?(cast_to_cell(v1), cast_to_cell(v2))) {
                equal? = 0;
            }
        }
        elseif (is_tuple(v1) & is_tuple(v2)) {
            ;; recursively determine nested tuples
            if (~ are_tuples_equal?(cast_to_tuple(v1), cast_to_tuple(v2))) {
                equal? = 0;
            }
        }
        else {
            equal? = 0;
        }

        i -= 1;
    }

    return equal?;
}

() main () {
    tuple t1 = cast_to_tuple([[2, 6], [1, [3, [3, 5]]], 3]);
    tuple t2 = cast_to_tuple([[2, 6], [1, [3, [3, 5]]], 3]);

    ~dump(are_tuples_equal?(t1, t2)); ;; -1 
}
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> ["cell_hash()" in docs](/develop/func/stdlib/#cell_hash)
>
> [ë¬¸ì„œì˜ 'TVM ì§€ì¹¨'](/í•™ìŠµ/tvm-ì§€ì¹¨/ì§€ì¹¨)

### ë‚´ë¶€ ì£¼ì†Œ ìƒì„±

ì»¨íŠ¸ë™íŠ¸ê°€ ìƒˆ ì»¨íŠ¸ë™íŠ¸ë¥¼ ë°°í¬í•´ì•¼ í•  ë•Œ ë‚´ë¶€ ì£¼ì†Œë¥¼ ìƒì„±í•´ì•¼ í•˜ëŠ”ë° ì£¼ì†Œë¥¼ ëª¨ë¦…ë‹ˆë‹¤. ìƒˆ ì»¨íŠ¸ë™íŠ¸ì˜ ì½”ë“œì™€ ë°ì´í„°ì¸ `state_init`ì´ ì´ë¯¸ ìˆë‹¤ê³  ê°€ì •í•´ë´…ì‹œë‹¤.

í•´ë‹¹ MsgAddressInt TLBì— ëŒ€í•œ ë‚´ë¶€ ì£¼ì†Œë¥¼ ìƒì„±í•©ë‹ˆë‹¤.

```func
(slice) generate_internal_address (int workchain_id, cell state_init) {
    ;; addr_std$10 anycast:(Maybe Anycast) workchain_id:int8 address:bits256  = MsgAddressInt;

    return begin_cell()
        .store_uint(2, 2) ;; addr_std$10
        .store_uint(0, 1) ;; anycast nothing
        .store_int(workchain_id, 8) ;; workchain_id: -1
        .store_uint(cell_hash(state_init), 256)
    .end_cell().begin_parse();
}

() main () {
    slice deploy_address = generate_internal_address(workchain(), state_init);
    ;; then we can deploy new contract
}
```

> ğŸ’¡ ì°¸ê³ 
>
> ì´ ì˜ˆì—ì„œëŠ” `workchain()`ì„ ì‚¬ìš©í•˜ì—¬ ì›Œí¬ì²´ì¸ì˜ IDë¥¼ ê°€ì ¸ì˜µë‹ˆë‹¤. ì›Œí¬ì²´ì¸ IDì— ëŒ€í•œ ìì„¸í•œ ë‚´ìš©ì€ [ë¬¸ì„œ](/í•™ìŠµ/ê°œìš”/ì£¼ì†Œ#ì›Œí¬ì²´ì¸-id)ì—ì„œ í™•ì¸í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> ["cell_hash()" in docs](/develop/func/stdlib/#cell_hash)

### ì™¸ë¶€ ì£¼ì†Œ ìƒì„±

ë¸”ë¡](https://github.com/ton-blockchain/ton/blob/24dc184a2ea67f9c47042b4104bbb4d82289fac1/crypto/block/block.tlb#L101C1-L101C12)ì˜ TL-B ì²´ê³„ë¥¼ ì‚¬ìš©í•˜ì—¬ ì´ í˜•ì‹ì˜ ì£¼ì†Œë¥¼ ìƒì„±í•˜ëŠ” ë°©ë²•ì„ ì´í•´í•©ë‹ˆë‹¤.

```func
(int) ubitsize (int a) asm "UBITSIZE";

slice generate_external_address (int address) {
    ;; addr_extern$01 len:(## 9) external_address:(bits len) = MsgAddressExt;
    
    int address_length = ubitsize(address);
    
    return begin_cell()
        .store_uint(1, 2) ;; addr_extern$01
        .store_uint(address_length, 9)
        .store_uint(address, address_length)
    .end_cell().begin_parse();
}
```

ì£¼ì†Œê°€ ì°¨ì§€í•˜ëŠ” ë¹„íŠ¸ ìˆ˜ë¥¼ ê²°ì •í•´ì•¼ í•˜ë¯€ë¡œ, ìˆ«ìë¥¼ ì €ì¥í•˜ëŠ” ë° í•„ìš”í•œ ìµœì†Œ ë¹„íŠ¸ ìˆ˜ë¥¼ ë°˜í™˜í•˜ëŠ” ì—°ì‚°ì `UBITSIZE`ë¥¼ ì‚¬ìš©í•˜ì—¬ [asm í•¨ìˆ˜ ì„ ì–¸](#how-to-write-own-functions-using-asm-keyword)ì„ í•´ì•¼ í•©ë‹ˆë‹¤.

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> [ë¬¸ì„œì˜ 'TVM ì§€ì¹¨'](/í•™ìŠµ/tvm-ì§€ì¹¨/ì§€ì¹¨#53-êµëŒ€-ë…¼ë¦¬ì -ì—°ì‚°)

### ë¡œì»¬ ì €ì¥ì†Œì— ì‚¬ì „ì„ ì €ì¥í•˜ê³  ë¡œë“œí•˜ëŠ” ë°©ë²•

ì‚¬ì „ì„ ë¡œë“œí•˜ëŠ” ë¡œì§

```func
slice local_storage = get_data().begin_parse();
cell dictionary_cell = new_dict();
if (~ slice_empty?(local_storage)) {
    dictionary_cell = local_storage~load_dict();
}
```

ì‚¬ì „ì„ ì €ì¥í•˜ëŠ” ë¡œì§ì€ ë‹¤ìŒ ì˜ˆì‹œì™€ ê°™ìŠµë‹ˆë‹¤:

```func
set_data(begin_cell().store_dict(dictionary_cell).end_cell());
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> ["get_data()" in docs](/develop/func/stdlib/#get_data)
>
> ["new_dict()" in docs](/develop/func/stdlib/#new_dict)
>
> ["slice_empty?()" in docs](/develop/func/stdlib/#slice_empty)
>
> ["load_dict()" in docs](/develop/func/stdlib/#load_dict)
>
> [ë¬¸ì„œì—ì„œ "~"](/ê°œë°œ/í€í¬/ìŠ¤í…Œì´íŠ¸ë¨¼íŠ¸#ìœ ë‹ˆíƒ€ë¦¬ ì—°ì‚°ì)

### ê°„ë‹¨í•œ ë©”ì‹œì§€ ë³´ë‚´ê¸°

ëŒ“ê¸€ê³¼ í•¨ê»˜ í†¤ì„ ì „ì†¡í•˜ëŠ” ì¼ë°˜ì ì¸ ë°©ë²•ì€ ì‚¬ì‹¤ ê°„ë‹¨í•œ ë©”ì‹œì§€ì…ë‹ˆë‹¤. ë©”ì‹œì§€ ë³¸ë¬¸ì´ 'ëŒ“ê¸€'ì„ì„ ì§€ì •í•˜ë ¤ë©´ ë©”ì‹œì§€ í…ìŠ¤íŠ¸ ì•ì˜ '32ë¹„íŠ¸'ë¥¼ 0ìœ¼ë¡œ ì„¤ì •í•´ì•¼ í•©ë‹ˆë‹¤.

```func
cell msg = begin_cell()
    .store_uint(0x18, 6) ;; flags
    .store_slice("EQBIhPuWmjT7fP-VomuTWseE8JNWv2q7QYfsVQ1IZwnMk8wL"a) ;; destination address
    .store_coins(100) ;; amount of nanoTons to send
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; default message headers (see sending messages page)
    .store_uint(0, 32) ;; zero opcode - means simple transfer message with comment
    .store_slice("Hello from FunC!") ;; comment
.end_cell();
send_raw_message(msg, 3); ;; mode 3 - pay fees separately, ignore errors
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> [[ë¬¸ì„œì˜ 'ë©”ì‹œì§€ ë ˆì´ì•„ì›ƒ'](/ê°œë°œ/ìŠ¤ë§ˆíŠ¸ê³„ì•½/ë©”ì‹œì§€)

### ìˆ˜ì‹  ê³„ì •ìœ¼ë¡œ ë©”ì‹œì§€ë¥¼ ë³´ë‚´ëŠ” ë°©ë²•

ì•„ë˜ ê³„ì•½ ì˜ˆì‹œëŠ” ì‚¬ìš©ìì™€ ì£¼ ê³„ì•½ ê°„ì— ì–´ë–¤ ì‘ì—…ì„ ìˆ˜í–‰í•´ì•¼ í•˜ëŠ” ê²½ìš°, ì¦‰ ëŒ€ë¦¬ ê³„ì•½ì´ í•„ìš”í•œ ê²½ìš°ì— ìœ ìš©í•©ë‹ˆë‹¤.

```func
() recv_internal (slice in_msg_body) {
    {-
        This is a simple example of a proxy-contract.
        It will expect in_msg_body to contain message mode, body and destination address to be sent to.
    -}

    int mode = in_msg_body~load_uint(8); ;; first byte will contain msg mode
    slice addr = in_msg_body~load_msg_addr(); ;; then we parse the destination address
    slice body = in_msg_body; ;; everything that is left in in_msg_body will be our new message's body

    cell msg = begin_cell()
        .store_uint(0x18, 6)
        .store_slice(addr)
        .store_coins(100) ;; just for example
        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; default message headers (see sending messages page)
        .store_slice(body)
    .end_cell();
    send_raw_message(msg, mode);
}
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> [[ë¬¸ì„œì˜ 'ë©”ì‹œì§€ ë ˆì´ì•„ì›ƒ'](/ê°œë°œ/ìŠ¤ë§ˆíŠ¸ê³„ì•½/ë©”ì‹œì§€)
>
> ["load_msg_addr()" in docs](/develop/func/stdlib/#load_msg_addr)

### ì „ì²´ ì”ì•¡ìœ¼ë¡œ ë©”ì‹œì§€ë¥¼ ë³´ë‚´ëŠ” ë°©ë²•

ìŠ¤ë§ˆíŠ¸ ì½˜íŠ¸ë™íŠ¸ì˜ ì „ì²´ ì”ì•¡ì„ ì „ì†¡í•´ì•¼ í•˜ëŠ” ê²½ìš°, ì´ ê²½ìš° 'ëª¨ë“œ 128'ì„ ì‚¬ìš©í•´ì•¼ í•©ë‹ˆë‹¤. ì´ëŸ¬í•œ ê²½ìš°ì˜ ì˜ˆë¡œëŠ” ê²°ì œë¥¼ ìˆ˜ë½í•˜ê³  ë©”ì¸ ì»¨íŠ¸ë™íŠ¸ë¡œ ì „ë‹¬í•˜ëŠ” í”„ë¡ì‹œ ì»¨íŠ¸ë™íŠ¸ë¥¼ ë“¤ ìˆ˜ ìˆìŠµë‹ˆë‹¤.

```func
cell msg = begin_cell()
    .store_uint(0x18, 6) ;; flags
    .store_slice("EQBIhPuWmjT7fP-VomuTWseE8JNWv2q7QYfsVQ1IZwnMk8wL"a) ;; destination address
    .store_coins(0) ;; we don't care about this value right now
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; default message headers (see sending messages page)
    .store_uint(0, 32) ;; zero opcode - means simple transfer message with comment
    .store_slice("Hello from FunC!") ;; comment
.end_cell();
send_raw_message(msg, 128); ;; mode = 128 is used for messages that are to carry all the remaining balance of the current smart contract
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> [[ë¬¸ì„œì˜ 'ë©”ì‹œì§€ ë ˆì´ì•„ì›ƒ'](/ê°œë°œ/ìŠ¤ë§ˆíŠ¸ê³„ì•½/ë©”ì‹œì§€)
>
> ["ë©”ì‹œì§€ ëª¨ë“œ" ë¬¸ì„œ](/develop/func/stdlib/#send_raw_message)

### ê¸´ í…ìŠ¤íŠ¸ ëŒ“ê¸€ì´ í¬í•¨ëœ ë©”ì‹œì§€ë¥¼ ë³´ë‚´ëŠ” ë°©ë²•

ì•„ì‹œë‹¤ì‹œí”¼, í•˜ë‚˜ì˜ 'ì…€'(<1023ë¹„íŠ¸)ì—ëŠ” 127ìë§Œ ë“¤ì–´ê°ˆ ìˆ˜ ìˆìŠµë‹ˆë‹¤. ë” ë§ì€ ë¬¸ìê°€ í•„ìš”í•œ ê²½ìš° ìŠ¤ë„¤ì´í¬ ì…€ì„ êµ¬ì„±í•´ì•¼ í•©ë‹ˆë‹¤.

```func
{-
    If we want to send a message with really long comment, we should split the comment to several slices.
    Each slice should have <1023 bits of data (127 chars).
    Each slice should have a reference to the next one, forming a snake-like structure.
-}

cell body = begin_cell()
    .store_uint(0, 32) ;; zero opcode - simple message with comment
    .store_slice("long long long message...")
    .store_ref(begin_cell()
        .store_slice(" you can store string of almost any length here.")
        .store_ref(begin_cell()
            .store_slice(" just don't forget about the 127 chars limit for each slice")
        .end_cell())
    .end_cell())
.end_cell();

cell msg = begin_cell()
    .store_uint(0x18, 6) ;; flags
    ;; We use literal `a` to get valid address inside slice from string containing address 
    .store_slice("EQBIhPuWmjT7fP-VomuTWseE8JNWv2q7QYfsVQ1IZwnMk8wL"a) ;; destination address
    .store_coins(100) ;; amount of nanoTons to send
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1) ;; default message headers (see sending messages page)
    .store_uint(1, 1) ;; we want to store body as a ref
    .store_ref(body)
.end_cell();
send_raw_message(msg, 3); ;; mode 3 - pay fees separately, ignore errors
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> [ë¬¸ì„œì˜ 'ë‚´ë¶€ ë©”ì‹œì§€'](/ê°œë°œ/ìŠ¤ë§ˆíŠ¸ê³„ì•½/ê°€ì´ë“œë¼ì¸/ë‚´ë¶€ ë©”ì‹œì§€)

### ìŠ¬ë¼ì´ìŠ¤ì—ì„œ ë°ì´í„° ë¹„íŠ¸ë§Œ ê°€ì ¸ì˜¤ëŠ” ë°©ë²•(ì°¸ì¡° ì—†ì´)

'ìŠ¬ë¼ì´ìŠ¤' ì•ˆì— ìˆëŠ” 'ì°¸ì¡°'ì— ê´€ì‹¬ì´ ì—†ë‹¤ë©´ ë³„ë„ì˜ ë‚ ì§œë¥¼ ê°€ì ¸ì™€ì„œ ì‘ì—…í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.

```func
slice s = begin_cell()
    .store_slice("Some data bits...")
    .store_ref(begin_cell().end_cell()) ;; some references
    .store_ref(begin_cell().end_cell()) ;; some references
.end_cell().begin_parse();

slice s_only_data = s.preload_bits(s.slice_bits());
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> [ë¬¸ì„œì—ì„œ "ìŠ¬ë¼ì´ìŠ¤ í”„ë¦¬ë¯¸í‹°ë¸Œ"](/ê°œë°œ/í€í¬/stdlib/#ìŠ¬ë¼ì´ìŠ¤ í”„ë¦¬ë¯¸í‹°ë¸Œ)
>
> ["preload_bits()" in docs](/develop/func/stdlib/#preload_bits)
>
> ["slice_bits()" in docs](/develop/func/stdlib/#slice_bits)

### ë‚˜ë§Œì˜ ìˆ˜ì • ë°©ë²•ì„ ì •ì˜í•˜ëŠ” ë°©ë²•

ìˆ˜ì • ë©”ì„œë“œë¥¼ ì‚¬ìš©í•˜ë©´ ë™ì¼í•œ ë³€ìˆ˜ ë‚´ì—ì„œ ë°ì´í„°ë¥¼ ìˆ˜ì •í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤. ì´ëŠ” ë‹¤ë¥¸ í”„ë¡œê·¸ë˜ë° ì–¸ì–´ì˜ ì°¸ì¡°ì™€ ë¹„êµí•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.

```func
(slice, (int)) load_digit (slice s) {
    int x = s~load_uint(8); ;; load 8 bits (one char) from slice
    x -= 48; ;; char '0' has code of 48, so we substract it to get the digit as a number
    return (s, (x)); ;; return our modified slice and loaded digit
}

() main () {
    slice s = "258";
    int c1 = s~load_digit();
    int c2 = s~load_digit();
    int c3 = s~load_digit();
    ;; here s is equal to "", and c1 = 2, c2 = 5, c3 = 8
}
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> ["ë¬¸ì„œì—ì„œ ë©”ì„œë“œ ìˆ˜ì •í•˜ê¸°"](/ê°œë°œ/í€í¬/ìŠ¤í…Œì´íŠ¸ë¨¼íŠ¸#ë©”ì„œë“œ ìˆ˜ì •í•˜ê¸°)

### ìˆ«ìë¥¼ nì˜ ê±°ë“­ì œê³±ìœ¼ë¡œ ì˜¬ë¦¬ëŠ” ë°©ë²•

```func
;; Unoptimized variant
int pow (int a, int n) {
    int i = 0;
    int value = a;
    while (i < n - 1) {
        a *= value;
        i += 1;
    }
    return a;
}

;; Optimized variant
(int) binpow (int n, int e) {
    if (e == 0) {
        return 1;
    }
    if (e == 1) {
        return n;
    }
    int p = binpow(n, e / 2);
    p *= p;
    if ((e % 2) == 1) {
        p *= n;
    }
    return p;
}

() main () {
    int num = binpow(2, 3);
    ~dump(num); ;; 8
}
```

### ë¬¸ìì—´ì„ intë¡œ ë³€í™˜í•˜ëŠ” ë°©ë²•

```func
slice string_number = "26052021";
int number = 0;

while (~ string_number.slice_empty?()) {
    int char = string_number~load_uint(8);
    number = (number * 10) + (char - 48); ;; we use ASCII table
}

~dump(number);
```

### intë¥¼ ë¬¸ìì—´ë¡œ ë³€í™˜í•˜ëŠ” ë°©ë²•

```func
int n = 261119911;
builder string = begin_cell();
tuple chars = null();
do {
    int r = n~divmod(10);
    chars = cons(r + 48, chars);
} until (n == 0);
do {
    int char = chars~list_next();
    string~store_uint(char, 8);
} until (null?(chars));

slice result = string.end_cell().begin_parse();
~dump(result);
```

### ì‚¬ì „ì„ ë°˜ë³µí•˜ëŠ” ë°©ë²•

ë”•ì…”ë„ˆë¦¬ëŠ” ë§ì€ ë°ì´í„°ë¡œ ì‘ì—…í•  ë•Œ ë§¤ìš° ìœ ìš©í•©ë‹ˆë‹¤. ë‚´ì¥ ë©”ì„œë“œ `dict_get_min?`ê³¼ `dict_get_max?`ë¥¼ ì‚¬ìš©í•˜ì—¬ ê°ê° ìµœì†Œ ë° ìµœëŒ€ í‚¤ ê°’ì„ ì–»ì„ ìˆ˜ ìˆìŠµë‹ˆë‹¤. ë˜í•œ `dict_get_next?`ë¥¼ ì‚¬ìš©í•˜ì—¬ ì‚¬ì „ì„ ë°˜ë³µí•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.

```func
cell d = new_dict();
d~udict_set(256, 1, "value 1");
d~udict_set(256, 5, "value 2");
d~udict_set(256, 12, "value 3");

;; iterate keys from small to big
(int key, slice val, int flag) = d.udict_get_min?(256);
while (flag) {
    ;; do something with pair key->val
    
    (key, val, flag) = d.udict_get_next?(256, key);
}
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> [ë¬¸ì„œì˜ "ë”•ì…”ë„ˆë¦¬ í”„ë¦¬ë¯¸í‹°ë¸Œ"](/ê°œë°œ/í€í¬/ìŠ¤íƒ ë‹¤ë“œ ë¼ì´ë¸ŒëŸ¬ë¦¬/#ë”•ì…”ë„ˆë¦¬-í”„ë¦¬ë¯¸í‹°ë¸Œ)
>
> ["dict_get_max?()" in docs](/develop/func/stdlib/#dict_get_max)
>
> ["dict_get_min?()" in docs](/develop/func/stdlib/#dict_get_min)
>
> ["dict_get_next?()" in docs](/develop/func/stdlib/#dict_get_next)
>
> ["dict_set()" in docs](/develop/func/stdlib/#dict_set)

### ì‚¬ì „ì—ì„œ ê°’ì„ ì‚­ì œí•˜ëŠ” ë°©ë²•

```func
cell names = new_dict();
names~udict_set(256, 27, "Alice");
names~udict_set(256, 25, "Bob");

names~udict_delete?(256, 27);

(slice val, int key) = names.udict_get?(256, 27);
~dump(val); ;; null() -> means that key was not found in a dictionary
```

### ì…€ íŠ¸ë¦¬ë¥¼ ì¬ê·€ì ìœ¼ë¡œ ë°˜ë³µí•˜ëŠ” ë°©ë²•

ì•„ì‹œë‹¤ì‹œí”¼, í•˜ë‚˜ì˜ 'ì…€'ì€ ìµœëŒ€ '1023ë¹„íŠ¸'ì˜ ë°ì´í„°ì™€ ìµœëŒ€ '4ê°œì˜ ì°¸ì¡°'ë¥¼ ì €ì¥í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤. ì´ í•œê³„ë¥¼ ê·¹ë³µí•˜ê¸° ìœ„í•´ ì…€ íŠ¸ë¦¬ë¥¼ ì‚¬ìš©í•  ìˆ˜ ìˆì§€ë§Œ, ì´ë¥¼ ìœ„í•´ì„œëŠ” ì ì ˆí•œ ë°ì´í„° ì²˜ë¦¬ë¥¼ ìœ„í•´ ë°˜ë³µí•  ìˆ˜ ìˆì–´ì•¼ í•©ë‹ˆë‹¤.

```func
forall X -> int is_null (X x) asm "ISNULL";
forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";

() main () {
    ;; just some cell for example
    cell c = begin_cell()
        .store_uint(1, 16)
        .store_ref(begin_cell()
            .store_uint(2, 16)
        .end_cell())
        .store_ref(begin_cell()
            .store_uint(3, 16)
            .store_ref(begin_cell()
                .store_uint(4, 16)
            .end_cell())
            .store_ref(begin_cell()
                .store_uint(5, 16)
            .end_cell())
        .end_cell())
    .end_cell();

    ;; creating tuple with no data, which plays the role of stack
    tuple stack = null();
    ;; bring the main cell into the stack to process it in the loop
    stack~push_back(c);
    ;; do it until stack is not null
    while (~ stack.is_null()) {
        ;; get the cell from the stack and convert it to a slice to be able to process it
        slice s = stack~pop_back().begin_parse();

        ;; do something with s data

        ;; if the current slice has any refs, add them to stack
        repeat (s.slice_refs()) {
            stack~push_back(s~load_ref());
        }
    }
}
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> [ë¬¸ì„œì˜ "Lisp ìŠ¤íƒ€ì¼ ëª©ë¡"](/ê°œë°œ/í€í¬/ìŠ¤íƒ ë‹¤ë“œ ë¼ì´ë¸ŒëŸ¬ë¦¬/#lisp-style-lists)
>
> ["null()" in docs](/develop/func/stdlib/#null)
>
> ["slice_refs()" in docs](/develop/func/stdlib/#slice_refs)

### ë¦¬ìŠ¤í”„ ìŠ¤íƒ€ì¼ ëª©ë¡ì„ ë°˜ë³µí•˜ëŠ” ë°©ë²•

ë°ì´í„° ìœ í˜• íŠœí”Œì€ ìµœëŒ€ 255ê°œì˜ ê°’ì„ ë‹´ì„ ìˆ˜ ìˆìŠµë‹ˆë‹¤. ì´ê²ƒìœ¼ë¡œ ì¶©ë¶„í•˜ì§€ ì•Šë‹¤ë©´ ë¦¬ìŠ¤í”„ ìŠ¤íƒ€ì¼ì˜ ëª©ë¡ì„ ì‚¬ìš©í•´ì•¼ í•©ë‹ˆë‹¤. íŠœí”Œ ì•ˆì— íŠœí”Œì„ ë„£ì–´ ì œí•œì„ ìš°íšŒí•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.

```func
forall X -> int is_null (X x) asm "ISNULL";
forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";

() main () {
    ;; some example list
    tuple l = null();
    l~push_back(1);
    l~push_back(2);
    l~push_back(3);

    ;; iterating through elements
    ;; note that this iteration is in reversed order
    while (~ l.is_null()) {
        var x = l~pop_back();

        ;; do something with x
    }
}
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> [ë¬¸ì„œì˜ "Lisp ìŠ¤íƒ€ì¼ ëª©ë¡"](/ê°œë°œ/í€í¬/ìŠ¤íƒ ë‹¤ë“œ ë¼ì´ë¸ŒëŸ¬ë¦¬/#lisp-style-lists)
>
> ["null()" in docs](/develop/func/stdlib/#null)

### ë°°í¬ ë©”ì‹œì§€ë¥¼ ë³´ë‚´ëŠ” ë°©ë²•(stateInitë§Œ ì‚¬ìš©, stateInitê³¼ ë³¸ë¬¸ í¬í•¨)

```func
() deploy_with_stateinit(cell message_header, cell state_init) impure {
  var msg = begin_cell()
    .store_slice(begin_parse(msg_header))
    .store_uint(2 + 1, 2) ;; init:(Maybe (Either StateInit ^StateInit))
    .store_uint(0, 1) ;; body:(Either X ^X)
    .store_ref(state_init)
    .end_cell();

  ;; mode 64 - carry the remaining value in the new message
  send_raw_message(msg, 64); 
}

() deploy_with_stateinit_body(cell message_header, cell state_init, cell body) impure {
  var msg = begin_cell()
    .store_slice(begin_parse(msg_header))
    .store_uint(2 + 1, 2) ;; init:(Maybe (Either StateInit ^StateInit))
    .store_uint(1, 1) ;; body:(Either X ^X)
    .store_ref(state_init)
    .store_ref(body)
    .end_cell();

  ;; mode 64 - carry the remaining value in the new message
  send_raw_message(msg, 64); 
}
```

### stateInit ì…€ì„ ë¹Œë“œí•˜ëŠ” ë°©ë²•

```func
() build_stateinit(cell init_code, cell init_data) {
  var state_init = begin_cell()
    .store_uint(0, 1) ;; split_depth:(Maybe (## 5))
    .store_uint(0, 1) ;; special:(Maybe TickTock)
    .store_uint(1, 1) ;; (Maybe ^Cell)
    .store_uint(1, 1) ;; (Maybe ^Cell)
    .store_uint(0, 1) ;; (HashmapE 256 SimpleLib)
    .store_ref(init_code)
    .store_ref(init_data)
    .end_cell();
}
```

### ì»¨íŠ¸ë™íŠ¸ ì£¼ì†Œë¥¼ ê³„ì‚°í•˜ëŠ” ë°©ë²•(stateInit ì‚¬ìš©)

```func
() calc_address(cell state_init) {
  var future_address = begin_cell() 
    .store_uint(2, 2) ;; addr_std$10
    .store_uint(0, 1) ;; anycast:(Maybe Anycast)
    .store_uint(0, 8) ;; workchain_id:int8
    .store_uint(cell_hash(state_init), 256) ;; address:bits256
    .end_cell();
}
```

### ìŠ¤ë§ˆíŠ¸ ì»¨íŠ¸ë™íŠ¸ ë¡œì§ì„ ì—…ë°ì´íŠ¸í•˜ëŠ” ë°©ë²•

ì•„ë˜ëŠ” ì¹´ìš´í„°ë¥¼ ì¦ê°€ì‹œí‚¤ê³  ìŠ¤ë§ˆíŠ¸ ì»¨íŠ¸ë™íŠ¸ ë¡œì§ì„ ì—…ë°ì´íŠ¸í•˜ëŠ” ê¸°ëŠ¥ì´ ìˆëŠ” ê°„ë‹¨í•œ `Ğ¡ounterV1` ìŠ¤ë§ˆíŠ¸ ì»¨íŠ¸ë™íŠ¸ì…ë‹ˆë‹¤.

```func
() recv_internal (slice in_msg_body) {
    int op = in_msg_body~load_uint(32);
    
    if (op == op::increase) {
        int increase_by = in_msg_body~load_uint(32);
        ctx_counter += increase_by;
        save_data();
        return ();
    }

    if (op == op::upgrade) {
        cell code = in_msg_body~load_ref();
        set_code(code);
        return ();
    }
}
```

ìŠ¤ë§ˆíŠ¸ ì»¨íŠ¸ë™íŠ¸ë¥¼ ì‘ë™í•œ í›„, ë¯¸í„°ê¸° ê°ì†Œ ê¸°ëŠ¥ì´ ëˆ„ë½ë˜ì—ˆë‹¤ëŠ” ê²ƒì„ ì•Œê²Œ ë©ë‹ˆë‹¤. ìŠ¤ë§ˆíŠ¸ ì»¨íŠ¸ë™íŠ¸ 'CounterV1'ì˜ ì½”ë“œë¥¼ ë³µì‚¬í•˜ê³  'ì¦ê°€' ê¸°ëŠ¥ ì˜†ì— ìƒˆë¡œìš´ 'ê°ì†Œ' ê¸°ëŠ¥ì„ ì¶”ê°€í•´ì•¼ í•©ë‹ˆë‹¤. ì´ì œ ì½”ë“œê°€ ë‹¤ìŒê³¼ ê°™ì´ ë³´ì…ë‹ˆë‹¤:

```func
() recv_internal (slice in_msg_body) {
    int op = in_msg_body~load_uint(32);
    
    if (op == op::increase) {
        int increase_by = in_msg_body~load_uint(32);
        ctx_counter += increase_by;
        save_data();
        return ();
    }

    if (op == op::decrease) {
        int decrease_by = in_msg_body~load_uint(32);
        ctx_counter -= increase_by;
        save_data();
        return ();
    }

    if (op == op::upgrade) {
        cell code = in_msg_body~load_ref();
        set_code(code);
        return ();
    }
}
```

ìŠ¤ë§ˆíŠ¸ ì»¨íŠ¸ë™íŠ¸ `CounterV2`ê°€ ì¤€ë¹„ë˜ë©´, ì´ë¥¼ ì˜¤í”„ì²´ì¸ì—ì„œ `ì…€`ë¡œ ì»´íŒŒì¼í•˜ê³  `CounterV1` ìŠ¤ë§ˆíŠ¸ ì»¨íŠ¸ë™íŠ¸ë¡œ ì—…ê·¸ë ˆì´ë“œ ë©”ì‹œì§€ë¥¼ ë³´ë‚´ì•¼ í•©ë‹ˆë‹¤.

```javascript
await contractV1.sendUpgrade(provider.sender(), {
    code: await compile('ContractV2'),
    value: toNano('0.05'),
});
```

> ğŸ’¡ ìœ ìš©í•œ ë§í¬
>
> [ê¸°ì¡´ ì£¼ì†Œì— ì½”ë“œë¥¼ ë‹¤ì‹œ ë°°í¬í•  ìˆ˜ ìˆë‚˜ìš”, ì•„ë‹ˆë©´ ìƒˆ ì»¨íŠ¸ë™íŠ¸ë¡œ ë°°í¬í•´ì•¼ í•˜ë‚˜ìš”?(/develop/howto/faq#is-it-possible-to-re-deploy-code-to-an-existing-address-or-does-it-have-to-be-deployed-as-ìƒˆ-ê³„ì•½ìœ¼ë¡œ)](/ê°œë°œ/ë°©ë²•/ì§ˆë¬¸)
>
> ["set_code()" in docs](/develop/func/stdlib#set_code)
